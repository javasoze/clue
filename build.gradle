plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
}

group = 'io.dashbase.clue'
def baseVersion = '1.0.0-10.2.2'
def isSnapshot = (project.findProperty('snapshot') ?: System.getenv('CLUE_SNAPSHOT'))?.toString()?.toBoolean()
version = isSnapshot ? "${baseVersion}-SNAPSHOT" : baseVersion
description = 'command line client for Apache Lucene'

ext {
    luceneVersion = '10.2.2'
    retrofit2Version = '2.5.0'
    micronautVersion = '5.0.0-M5'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://repository.apache.org/snapshots/' }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'io.dashbase.clue.ClueApplication'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/javasoze/clue')
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
}

dependencies {
    implementation "org.apache.lucene:lucene-core:${luceneVersion}"
    implementation "org.apache.lucene:lucene-codecs:${luceneVersion}"
    implementation "org.apache.lucene:lucene-analysis-common:${luceneVersion}"
    implementation "org.apache.lucene:lucene-backward-codecs:${luceneVersion}"
    implementation "org.apache.lucene:lucene-queries:${luceneVersion}"
    implementation "org.apache.lucene:lucene-queryparser:${luceneVersion}"
    implementation "org.apache.lucene:lucene-facet:${luceneVersion}"

    // https://mvnrepository.com/artifact/tools.jackson.core/jackson-databind
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
// https://mvnrepository.com/artifact/tools.jackson.dataformat/jackson-dataformat-yaml
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.1'
    annotationProcessor "io.micronaut:micronaut-inject-java:${micronautVersion}"
    implementation "io.micronaut:micronaut-runtime:${micronautVersion}"
    implementation "io.micronaut:micronaut-http:${micronautVersion}"
    implementation "io.micronaut:micronaut-http-server:${micronautVersion}"
    implementation "io.micronaut:micronaut-http-server-netty:${micronautVersion}"
    implementation "io.micronaut:micronaut-jackson-databind:${micronautVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:2.1.1"
    runtimeOnly "ch.qos.logback:logback-classic:1.4.14"
    runtimeOnly "ch.qos.logback:logback-core:1.4.14"

    implementation "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    implementation "com.squareup.retrofit2:converter-scalars:${retrofit2Version}"
    implementation "com.squareup.retrofit2:converter-jackson:${retrofit2Version}"

    implementation 'jline:jline:2.12'
    implementation 'info.picocli:picocli:4.7.5'
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.0'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('copyRuntimeLibs', Copy) {
    from configurations.runtimeClasspath
    into layout.buildDirectory.dir('libs')
}

tasks.named('startScripts') {
    dependsOn tasks.named('copyRuntimeLibs')
}

tasks.named('assemble') {
    dependsOn tasks.named('copyRuntimeLibs')
}

tasks.named('test') {
    useJUnitPlatform()
}
